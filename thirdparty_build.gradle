/*CHANGE necessary properties in project.gradle*/
apply from: 'project.gradle'
defaultTasks 'buildNativeLibs'

buildscript {
    repositories { mavenCentral() }
}

/*DJVU CHAIN*/
task downloadDjvu {
    doLast {
        def djvuFolder = new File(djvu)
        if (djvuFolder.exists()) {
            // Switch to master so we can properly pull if it was
            // detached.
            exec {
                commandLine = ['git', '-C', djvu, 'checkout', '-f', 'master']
            }
            // Fetch
            exec {
                commandLine = ['git', '-C', djvu, 'fetch', 'origin']
            }
        }
        else {
            exec {
                //origin at git://git.code.sf.net/p/djvu/djvulibre-git
                commandLine = ['git', 'clone', 'https://github.com/max-kammerer/djvulibre.git', djvu]
            }
        }

        exec {
            workingDir file(djvu)
            commandLine = ['git', 'checkout', '2ad2b702d864d1974f0c569a7594b27e67c64a40']
        }
    }
}

task patchDjvu  {
    doLast {
//        exec {
//            workingDir file(djvu)
//            commandLine = ['git', 'apply', '../../djvu.patch']
//        }
        println "Task is obsolete"
    }
}

task downloadAndPatchDjvu(dependsOn: [downloadDjvu, patchDjvu])

task buildDjvu() {
    doLast {
        exec {
            workingDir file(djvuModule)
            commandLine ndkDir + "/ndk-build"
        }
    }
}

task djvuChain(dependsOn: [downloadAndPatchDjvu, buildDjvu])

patchDjvu.mustRunAfter downloadDjvu
buildDjvu.mustRunAfter patchDjvu


/*MUPDF CHAIN*/
task downloadMupdf {
    def version = "1.23.3"

    doLast {
        def mupdfFolder = new File(mupdf)
        if (mupdfFolder.exists()) {
            // Switch to master so we can properly pull if it was
            // detached.
            exec {
                commandLine = ['git', '-C', mupdf, 'checkout', '-f', 'master']
            }
            // Fetch
            exec {
                commandLine = ['git', '-C', mupdf, 'fetch', 'origin']
            }
        }
        else {
            exec {
                commandLine = ['git', 'clone', 'git://git.ghostscript.com/mupdf.git', mupdf]
            }
        }

        exec {
            workingDir file(mupdf)
            commandLine = ['git', 'checkout', version]
        }

        exec {
            workingDir file(mupdf)
            commandLine 'git', 'submodule', 'init'
        }
        exec {
            workingDir file(mupdf)
            commandLine 'git', 'submodule', 'update'
        }
    }
}

task downloadAndMakeMupdf(dependsOn: downloadMupdf)  {
    doLast {
        /*For more details see mupdf/platform/android/viewer/ReadMe.txt*/
        exec {
            workingDir file(mupdf)
            commandLine 'make', 'generate'
        }
    }
}

task buildMupdf() {
    doLast {
        exec {
            workingDir file(mupdfModule)
            commandLine ndkDir + "/ndk-build"
        }
    }
}

task mupdfChain(dependsOn: [downloadAndMakeMupdf, buildMupdf])

mupdfChain.mustRunAfter djvuChain


/*!!!!!!!!!!RUN ME!!!!!!!!*/
task buildNativeLibs(dependsOn: [djvuChain, mupdfChain])
