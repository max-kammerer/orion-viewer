apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: 'version.gradle'
apply from: '../project.gradle'

println "do singing: " + doSigning
if (doSigning) {
    apply from: 'signing.gradle'
}

dependencies {
    compile 'com.android.support:appcompat-v7:26.0.2'
    compile 'com.android.support:design:26.0.2'
    compile project(':tree-view-list-android')
    //compile project(':common')

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    //compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.18'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        versionCode = orionVersionCode
        versionName = orionVersionName

        minSdkVersion 14
        targetSdkVersion 23

        testApplicationId "universe.constellation.orion.viewer.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        archivesBaseName = "orion-viewer-$versionName"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = [mupdf_libs, djvu_libs]
            assets.srcDirs = ['assets']
            java.srcDirs += '../common/src/main/kotlin'
        }

        //test
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
        }
    }

    buildTypes {
        release {
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    if (variant.name == "release") {
                        outputFileName = "orion-viewer--${variant.version}-${variant.name}.apk"
                    }
                }
            }

            if (doSigning) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "arch"

    productFlavors {
        arm5 {
            versionCode = 0
            ndk {
                abiFilter "armeabi"
            }
            dimension "arch"
        }
        arm7 {
            versionCode = 1
            ndk {
                abiFilter "armeabi-v7a"
            }
            dimension "arch"
        }
        x86 {
            versionCode = 2
            ndk {
                abiFilter "x86"
            }
            dimension "arch"
        }
        mips {
            versionCode = 3
            ndk {
                abiFilter "mips"
            }
            dimension "arch"
        }
    }

    // make per-variant version code
    applicationVariants.all { variant ->
        // get the version code of each flavor
        def abiVersion = variant.productFlavors.get(0).versionCode

        // set the composite code
        variant.mergedFlavor.versionCode = abiVersion + defaultConfig.versionCode

        println "New version code for " + variant.productFlavors.name + " is " + variant.mergedFlavor.versionCode
    }

    lintOptions { abortOnError false }
}
kotlin {
    experimental {
        coroutines "enable"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xmulti-platform"]
    }
}