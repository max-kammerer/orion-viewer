apply plugin: 'com.android.application'
apply plugin: 'kotlin-multiplatform'
apply from: 'version.gradle'
apply from: '../project.gradle'

println "do singing: " + doSigning
if (doSigning) {
    apply from: 'signing.gradle'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    //tab layout
    implementation 'com.google.android.material:material:1.1.0'
    implementation project(':tree-view-list-android')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'
    implementation project(":nativeLibs:djvuModule")
    implementation project(":nativeLibs:mupdfModule")

    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'


    //MPP test workaround
    implementation project(":common")
}

android {
    if (orionNdkVersion != null) {
        ndkVersion orionNdkVersion
    }
    compileSdkVersion orionCompileSdk
    buildToolsVersion orionBuildToolsVersion

    def githubBuildNumber = (System.getenv("GITHUB_RUN_NUMBER") ?: "1").toInteger()

    defaultConfig {
        versionCode = orionVersionCode
        versionName = orionVersionName

        minSdkVersion orionMinSdk
        targetSdkVersion orionTargetSdk


        applicationId "universe.constellation.orion.viewer"
        testApplicationId "universe.constellation.orion.viewer.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        if (isCIBuild) {
            archivesBaseName = "orion-viewer-$versionName-$githubBuildNumber"
        } else {
            archivesBaseName = "orion-viewer-$versionName"
        }
    }

    sourceSets {
        main {
            assets.srcDirs = ['assets']
            //java.srcDirs += '../common/src/main/kotlin'
            if (excludeNativeLibsSources) {
                jniLibs.srcDirs = [mupdf_libs, djvu_libs]
            }
            java {
                srcDirs += "../nativeLibs/mupdf/platform/java/src"

                if (onyxSdkPath != null) {
                    srcDirs += onyxSdkPath + "/src/main/java/"
                }
            }

        }

        //test
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
            resources.srcDirs += 'src/androidTest/assets'
        }
    }

    buildTypes {
        release {
            if (doSigning) {
                signingConfig signingConfigs.release
            }
        }

        ci {
            matchingFallbacks = ['debug']
            debuggable = true
            applicationIdSuffix ".ci"
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "arch"

    productFlavors {
//        arm5 {
//            versionCode = 1
//            ndk {
//                abiFilter "armeabi"
//            }
//            dimension "arch"
//        }
        arm7 {
            versionCode = 1
            ndk {
                abiFilter "armeabi-v7a"
            }
            dimension "arch"
        }

        arm64 {
            versionCode = 2
            ndk {
                abiFilter "arm64-v8a"
            }
            dimension "arch"
        }
        x86 {
            versionCode = 3
            ndk {
                abiFilter "x86"
            }
            dimension "arch"
        }
        x86_64 {
            versionCode = 4
            ndk {
                abiFilter "x86_64"
            }
            dimension "arch"
        }
//        mips {
//            versionCode = 4
//            ndk {
//                abiFilter "mips"
//            }
//            dimension "arch"
//        }
    }

    // make per-variant version code
    applicationVariants.all { variant ->
        // get the version code of each flavor
        def abiVersion = variant.productFlavors.get(0).versionCode

        // set the composite code
        variant.outputs.each { output ->
            output.versionCodeOverride = (variant.buildType.name == "ci" ? githubBuildNumber : defaultConfig.versionCode) + abiVersion - 1
            println "New version code for " + variant.buildType.name + "/" + variant.productFlavors.name + " is " + output.versionCodeOverride
        }

    }

    lintOptions { abortOnError false }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
                implementation project(":common")
            }
        }

//        linuxAppMain {
//            dependencies {
//                implementation 'org.jetbrains.kotlin:kotlin-stdlib-native'
//            }
//
//            kotlin.srcDir('../native/src/main')
//        }

    }

    targets {
        fromPreset(presets.android, 'androidApp') {

        }
//        fromPreset(presets.linuxX64, 'linuxApp') {
//            compilations.main {
//                cinterops {
//                    interopName {
//                         defFile "../native/src/main/c_interop/gtk3.def"
//                    }
//                }
//            }
//        }
    }
}

task downloadTestFiles() {
    doFirst {
        testFiles.forEach {
            def name = it.tokenize('/').last()
            def fileName = project.rootDir.path + "/orion-viewer/src/androidTest/assets/testData/" + name
            def file = new File(fileName)
            if (!file.exists()) {
                println "Downloading " + it + " into " + file.absolutePath
                new URL(it).withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
    }
}


//afterEvaluate {
//    tasks.each {
//        if (it.name.contains("pre") && it.name.endsWith("AndroidTestBuild")) {
//            it.dependsOn 'downloadTestFiles'
//        }
//    }
//}